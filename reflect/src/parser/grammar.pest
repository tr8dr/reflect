// This is a grammar for constructor expressions such as:
// ```
//   Resample(Momentum(SMA, [100,50,20], [0.2, 0.3, 0.,5]), 900)
// ```
//

WHITESPACE = _{ " " | "\t" | "\n" | "\r" }

integer = @{ ASCII_DIGIT+ }
float = @{ ASCII_DIGIT+ ~ "." ~ ASCII_DIGIT* ~ (^"e" ~ ASCII_DIGIT+)? }
identifier = @{ ASCII_ALPHA ~ (ASCII_ALPHANUMERIC | "_")* }

primitive = _{ float | integer | identifier }
list = { "[" ~ primitive ~ ("," ~ primitive)* ~ "]" }

ctor_expression = { identifier ~ "(" ~ argument_list ~ ")" }
argument = _{ ctor_expression | list | primitive }
argument_list = _{ argument ~ ("," ~ argument)* }

expression = _{ SOI ~ (ctor_expression | primitive) ~ EOI }
